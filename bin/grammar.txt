program        → declaration* EOF ;
declaration    → varDeclaration | funDeclaration | statement ;
varDeclaration → "var" IDENTIFIER ( "=" expression )? ";" ;
funDeclaration → "fun" IDENTIFIER "(" parameters? ")" block ;
parameters     → IDENTIFIER ( "," IDENTIFIER ) * ;
statement      → exprStmt | forStmt | ifStmt | printStmt | whileStmt | breakStmt | block ;
whileStmt      → "while" "(" expression ")" statement ;
breakStmt      → "break" ";" ;
block          → "{" declaration* "}" ;
forStmt        → "for" "(" ( varDeclatation | exprStmt | ";" ) expression? ";" expression? ")" statement ;
ifStmt         → "if" "(" expression ")" statement ( "else" statement )? ;
exprStmt       → expression ";" ;
printStmt      → "print" expression ";" ;
expression     → assignment ;
assignment     → IDENTIFIER "=" assignment | logical_or ;
logical_or     → logical_and ( "or" logical_and ) * ;
logical_and    → equality ( "and" equality ) * ;
equality       → comparison (( "!=" | "==" ) comparison) * ;
comparison     → addition (( "<" | "<=" | ">" | ">=" ) addition) * ;
addition       → multiplication (( "-" | "+" ) multiplication) * ;
multiplication → unary (( "*" | "/" ) unary) * ;
unary          → (( "-" | "!" ) unary) | call ;
call           → primary ( "(" arguments? ")" )* ;
arguments      → expression ( "," expression )* ;
primary        → "false" | "true" | "nil" | NUMBER | STRING | "(" expression ")" | IDENTIFIER ;



/* condensed, no precedence
expression -> literal | unary | binary | grouping ;
literal -> NUMBER | STRING | "true" | "false" | "nil" ;
grouping -> "(" expression ")" ;
unary -> ( "-" | "!" ) expression ;
binary -> expression operator expression ;
operator -> "==" | "!=" | "<" | "<=" | ">" | ">=" | "+" | "-" | "*" | "/" ;
*/
